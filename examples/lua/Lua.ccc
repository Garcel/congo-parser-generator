#if false
   This is a grammar for the Lua programming language.
   It may be used for any purpose.
#endif

PARSER_PACKAGE=org.parsers.lua;
DEFAULT_LEXICAL_STATE=LUA;
TAB_SIZE=4;

INCLUDE "LuaLexer.ccc"

#Root : Block <EOF> {return CURRENT_NODE;};

Block#(>=0) : (Statement)* [LastStatement] ;

Statement :
   <SEMICOLON> #EmptyStatement(1)
   |
   Assignment
   |
   FunctionCall
   |
   Label
   |
   <BREAK> #BreakStatement(1)
   |
   GotoStatement
   |
   DoBlock 
   |
   WhileStatement 
   |
   RepeatStatement
   |
   IfStatement
   |
   ForStatement
   |
   FunctionDeclaration
   |
   LocalFunctionDeclaration
   |
   LocalAttributeAssignment
;

Assignment : VarList "=" =>|| ExpressionList ;

DoBlock : "do" Block "end" ;

GotoStatement : "goto" <NAME> ;

ForStatement :
   "for" <NAME> "=" =>|| Expression "," Expression ["," Expression] DoBlock
   |
   "for" NameList "in" =>|| ExpressionList DoBlock
   |
   "for" FAIL "invalid for statement"
;

FunctionDeclaration : "function" FunctionName FunctionBody ;

LocalFunctionDeclaration : "local" "function" =>|| <NAME> FunctionBody ;

LocalAttributeAssignment : "local" =>|+1 AttributeNameList ["=" ExpressionList] ;

IfStatement : "if" Expression "then" Block ("elseif" Expression "then" Block)* ["else" Block] "end" ;

RepeatStatement : "repeat" Block "until" Expression ;

WhileStatement : "while" Expression "do" Block "end" ;

AttributeNameList : <NAME> [Attribute] ("," <NAME> [Attribute])* ;

Attribute : "<" <NAME> ">" ;

LastStatement : "return" [ExpressionList] | "break" | "continue" [<SEMICOLON>] ;

Label : "::" <NAME> "::" ;

FunctionName : <NAME> ("." <NAME>)* [":" <NAME>] ;

VarList : Var ("," Var)* ;

NameList : <NAME> ("," <NAME>)* ;

ExpressionList : Expression ("," Expression)* ;

Literal#void : "nil" | "false" | "true" | Number | StringLiteral | "..." ; 

PrimaryExpression : Literal | FunctionDef | PrefixExp | TableConstructor ;

PowerExpression : PrimaryExpression ("^" PowerExpression)* ;

UnaryExpression : (OperatorUnary)* PowerExpression ;

MultiplicativeExpression : UnaryExpression (OperatorMulDivMod UnaryExpression)* ;

AdditiveExpression : MultiplicativeExpression (OperatorAddSub MultiplicativeExpression)* ;

StringCatExpression : MultiplicativeExpression (OperatorStrCat StringCatExpression)* ;

ComparisonExpression : StringCatExpression (OperatorComparison ComparisonExpression)* ;

AndExpression : ComparisonExpression ("and" ComparisonExpression)* ;

OrExpression : AndExpression ("or" AndExpression)* ;

Expression : OrExpression (OperatorBitwise OrExpression)* ;

PrefixExp : VarOrExp (NameAndArgs)* ;

FunctionCall : VarOrExp (NameAndArgs)+ ;

//VarOrExp : Var | "(" Expression ")" ;
VarOrExp : (<NAME> | "(" Expression ")" ) (VarSuffix)* ;


Var : (<NAME> | "(" Expression ")" VarSuffix) (VarSuffix)* ;

VarSuffix : (NameAndArgs)* ("[" Expression "]" | "." <NAME>) ;

NameAndArgs : [":" <NAME>] Args ;

Args : "(" [ExpressionList] ")" | TableConstructor | StringLiteral ;

FunctionDef : "function" FunctionBody ;

FunctionBody : "(" [ParamList] ")" Block "end" ;

ParamList : NameList ["," | "..."] | "..." ;

TableConstructor : "{" [FieldList]  "}" ;

FieldList : Field (FieldSep Field)* [FieldSep] ;

Field : "[" Expression "]" "=" Expression | <NAME> "=" Expression | Expression ;

FieldSep : "," | <SEMICOLON> ;

OperatorComparison : "<" | ">" | "<=" | ">=" | "~=" | "==" ;

OperatorStrCat : ".." ;

OperatorAddSub : "+" | "-" ;

OperatorMulDivMod : "*" | "/" | "%" | "//" ;

OperatorBitwise : "&" | "|" | "~" | "<<" | ">>" ;

OperatorUnary : "not" | "#" | "-" | "~" ;

OperatorPower : "^" ;

Number : <INT> | <HEX> | <FLOAT> | <HEX_FLOAT> ;

StringLiteral : <NORMALSTRING> | <CHARSTRING> | <LONGSTRING> ;



