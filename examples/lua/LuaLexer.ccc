DEFAULT_LEXICAL_STATE=LUA;
TAB_SIZE=4;

TOKEN #Delimiter :
   <SEMICOLON: ";">
 | <COMMA: ",">
 | <LPAREN: "(">
 | <RPAREN: ")">
 | <LBRACE: "{">
 | <RBRACE: "}">
 | <LBRACKET : "[">
 | <RBRACKET : "]">
;

TOKEN #Operator :
   <ASSIGN : "=">
 | <LT: "<">
 | <LE : "<=">
 | <GT: ">">
 | <GE : ">=">
 | <COLON : ":">
 | <DOUBLE_COLON : "::">
 | <DOT : ".">
 | <ELLIPSIS : "...">
 | <HAT : "^">
 | <NE : "~=">
 | <EQ : "==">
 | <STR_CAT : "..">
 | <PLUS : "+">
 | <MINUS : "-">
 | <TIMES : "*">
 | <SLASH : "/">
 | <DOUBLE_SLASH : "//">
 | <MOD : "%">
 | <BIT_OR : "|">
 | <BIT_AND : "&">
 | <TILDE : "~">
 | <LSHIFT : "<<">
 | <RSHIFT  : ">>">
 | <HASH : "#">
 | <OR: "or"> 
 | <AND: "and">
 | <NOT : "not">
;  

TOKEN #Literal :
  <NORMALSTRING : '"' ( <ESCAPE_SEQUENCE> | (~['\\', '"']) )* '"'>
  |
  <CHARSTRING : "'" (<ESCAPE_SEQUENCE> | (~['\\',"'"]))* "'" >
  |
  <LONGSTRING : <LONGSTRING1> | <LONGSTRING2>>
  |
  <#LONGSTRING1 : '[[' (~["]"] | ("]" ~["]"]))* ']]' >
  |
  <#LONGSTRING2 : "[=" ("=")* "[" (~["]"] | (("]")+ ~["="]))* "]=" ("=")* "]">
  |
  <#NESTED_STR : ('=')* '[' (~[']'])* ']' ('=')* > 
  |
  <#DIGIT : ["0"-"9"]>
  |
  <INT : (<DIGIT>)+ >
  |
  <HEX : "0" ["x", "X"] (<HEX_DIGIT>)+ >
  |
  <FLOAT : 
      (<INT> "." (<DIGIT>)* (<EXPONENT_PART>)?)
      |
      ("." <INT> (<EXPONENT_PART>)?)
      |
      (<INT> <EXPONENT_PART>)
  >
  |
  <HEX_FLOAT :
      ("0" ["x", "X"] (<HEX_DIGIT>)+ "." (<HEX_DIGIT>)* (<HEX_EXPONENT_PART>)?)
      |
      ( "0" ["x", "X"] "." (<HEX_DIGIT>)+ (<HEX_EXPONENT_PART>)?) 
      |
      ( "0" ["x", "X"] (<HEX_DIGIT>)+ <HEX_EXPONENT_PART>)
  >
  |
  <#EXPONENT_PART : ["e", "E"] (["+", "-"])? <INT> >
  |
  <#HEX_EXPONENT_PART : ["p", "P"] (["+","-"])? <INT> >
  |
  <#ESCAPE_SEQUENCE : 
     ("\\" ["a","b","f","n","r","t","v","z","'", '"', "|", "$", "#", "\\"])
     |
     ("\\" (["\r"])? "\n")
     |
     <DECIMAL_ESCAPE>
     |
     <HEX_ESCAPE>
     |
     <UTF_ESCAPE>
  >
  |
  <#DECIMAL_ESCAPE : 
     "\\"
     (
        (["0"-"2"] <DIGIT> <DIGIT>)
        |
        (<DIGIT> (<DIGIT>)?)
     )
  >
  |
  <#HEX_ESCAPE : "\\x" <HEX_DIGIT> <HEX_DIGIT> >
  |
  <#UTF_ESCAPE : "\\u{" (<HEX_DIGIT>)+ "}" >
  |
  <#HEX_DIGIT : ["0"-"9", "a"-"f", "A"-"F"] >
;


TOKEN #KeyWord :
   <BREAK : "break">
// | <CONTINUE : "continue">
 | <DO : "do">
 | <ELSE : "else">
 | <ELSEIF : "elseif">
 | <END : "end">
 | <FALSE : "false">
 | <FOR : "for">
 | <FUNCTION : "function">
 | <GOTO : "goto">
 | <IF : "if">
 | <IN : "in">
 | <LOCAL : "local">
 | <NIL : "nil">
 | <REPEAT : "repeat">
 | <RETURN : "return">
 | <THEN : "then">
 | <TRUE : "true">
 | <UNTIL : "until" >
 | <WHILE : "while" >
;

TOKEN : 
    <NAME : ["a"-"z","A"-"Z", "_"](["a"-"z","A"-"Z","0"-"9","_"])*> #Name 
    |
    <SHEBANG : "#" (<SINGLE_LINE_INPUT_CHARACTER>)*>
    |
    <#SINGLE_LINE_INPUT_CHARACTER : ~["\r","\n","\u0085","\u2028","\u2029"] >
;


SKIP :
 <WS : ([" ", "\t", "\u000C", "\r", "\n"])+>
;

MORE :
 <SINGLE_LINE_COMMENT_START : "--" > : IN_SINGLE_LINE_COMMENT
 |
 <MULTILINE1_START : "--[["> : IN_MULTI_LINE1
 |
 <MULTILINE2_START : "--[" ("=")+> : IN_MULTI_LINE2
 
;

<IN_SINGLE_LINE_COMMENT> 
UNPARSED :
  < LINE_COMMENT : "\n" | "\r" | "\r\n" > #SingleLineComment : LUA
;

<IN_MULTI_LINE1>
UNPARSED :
  < MULTI1 : "]]" > #MultiLineComment : LUA
;

<IN_MULTI_LINE2>
UNPARSED :
  < MULTI2 : "]" ("=")+ "]" > #MultiLineComment : LUA
;

<IN_SINGLE_LINE_COMMENT, IN_MULTI_LINE1, IN_MULTI_LINE2>
MORE :
  < ANY_CHAR : ~[] >
;

