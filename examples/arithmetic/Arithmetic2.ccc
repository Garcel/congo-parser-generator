PARSER_PACKAGE=ex2;
PARSER_CLASS=Calc;
MINIMAL_TOKEN;

INCLUDE "Arithmetic1.ccc"

INJECT NUMBER :
{
    public double evaluate() {
        return Double.parseDouble(getImage());
    }
}

INJECT AdditiveExpression :
{
    public double evaluate() {
        double result = get(0).evaluate();
        for (int i=1; i< size(); i+=2) {
            boolean subtract = get(i) instanceof MINUS;
            double nextOperand = get(i+1).evaluate();
            if (subtract) result -= nextOperand;
            else result += nextOperand;
        }
        return result;
    }
}

INJECT MultiplicativeExpression :
{
    public double evaluate() {
        double result = get(0).evaluate();
        for (int i=1; i< size(); i+=2) {
            boolean divide = get(i) instanceof DIVIDE;
            double nextOperand = get(i+1).evaluate();
            if (divide) result /= nextOperand;
            else result *= nextOperand;
        }
        return result;
    }
}

INJECT ParentheticalExpression : 
{
    public double evaluate() {
        return get(1).evaluate();
    }
}

INJECT Root :
{
    public double evaluate() {
        return get(0).evaluate();
    }
}